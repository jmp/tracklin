plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'io.gitlab.arturbosch.detekt' version '1.7.4'
}

application {
    mainClassName = 'com.github.jmp.tracklin.MainKt'
}

javafx {
    version = '14'
    modules = [
        'javafx.controls',
        'javafx.fxml',
    ]
}

group 'com.github.jmp.tracklin'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven { url = 'https://raw.github.com/kristian/system-hook/mvn-repo/' }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.3.5'
    implementation 'lc.kra.system:system-hook:3.6'
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')
}
